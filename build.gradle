plugins {
    id 'java-library'
    id 'checkstyle'
    id "com.vanniktech.maven.publish" version "0.36.0"
}

group = 'io.github.khezyapp'
// If -Pversion is passed (CI), use it. Otherwise, default to snapshot (Local).
version = project.findProperty('version') ?: '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

checkstyle {
    toolVersion = "13.1.0"
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
        sarif.required = true
    }
}

tasks.withType(Javadoc).configureEach {
    options {
        // This stops the "warning: no comment" and HTML syntax messages.
        addStringOption('Xdoclint:none', '-quiet')
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "TestRepo"
            url = layout.buildDirectory.dir("repos")
        }
    }
}

// Docs: https://vanniktech.github.io/gradle-maven-publish-plugin/central/
mavenPublishing {
    // 1. Tell the plugin what to publish
    configureBasedOnAppliedPlugins()

    // 2. Deployment Targets (Replaces your repositories block)
    // For the new Central Portal (central.sonatype.com)
    publishToMavenCentral(true)

    // 3. Signing (Replaces your signing block)
    signAllPublications()

    // 4. POM Metadata (The plugin needs this to generate the POM)
    pom {
        name = 'Object Checksum'
        description = 'A reflection-based utility for generating deterministic checksums of complex Java objects.'
        url = 'https://github.com/khezyapp/object-checksum'
        licenses {
            license {
                name = 'The MIT License'
                url = 'https://opensource.org/licenses/MIT'
            }
        }
        developers {
            developer {
                id = 'khezyapp'
                name = 'Khezy'
                email = 'khezy.app@gmail.com'
            }
        }
        scm {
            connection = 'scm:git:git://github.com/khezyapp/object-checksum.git'
            developerConnection = 'scm:git:ssh://github.com:khezyapp/object-checksum.git'
            url = 'https://github.com/khezyapp/object-checksum'
        }
    }
}